#lang typed/racket/base

;; This module defines facilities for asserting and querying assumptions
;; about primitive operations, namely:
;; - implications and exclusions between primitive predicates
;; - (conservative) ranges of primitive operations
;; - arities of primitive operations

(require racket/match
         racket/set
         (only-in racket/list remove-duplicates)
         typed/racket/unit
         set-extras
         "../utils/def.rkt"
         "../utils/map.rkt"
         "../ast/main.rkt"
         "../runtime/main.rkt"
         "signatures.rkt")

(provide local-prover-base@)

(define-unit local-prover-base@
  (import)
  (export local-prover-base^)

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ;;;;; Implication and Exclusion
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (define implication-table : (HashTable Symbol (‚Ñò Symbol)) (make-hasheq))
  (define exclusion-table : (HashTable Symbol (‚Ñò Symbol)) (make-hasheq))
  (define implication-table‚Åª¬π : (HashTable Symbol (‚Ñò Symbol)) (make-hasheq))

  ;; Extend implication table and take care of transitivity
  (define (add-implication! [p : Symbol] [q : Symbol]) : Void
    (unless (map-has? implication-table p q)
      (map-add! implication-table   p q #:eq? #t)
      (map-add! implication-table‚Åª¬π q p #:eq? #t)
      ;; implication is reflexive
      (add-implication! p p)
      (add-implication! q q)
      ;; implication is transitive
      (for ([q* (in-set (get-weakers q))])
        (add-implication! p q*))
      (for ([p‚ÇÄ (in-set (get-strongers p))])
        (add-implication! p‚ÇÄ q))
      ;; (r ‚Üí ¬¨q) and (q‚ÇÄ ‚Üí q) implies r ‚Üí ¬¨q‚ÇÄ
      (for* ([r (in-set (get-exclusions q))])
        (add-exclusion! p r))))

  ;; Extend exclusion table and take care of inferring existing implication
  (define (add-exclusion! [p : Symbol] [q : Symbol]) : Void
    (unless (map-has? exclusion-table p q)
      (map-add! exclusion-table p q #:eq? #t)
      ;; (p ‚Üí ¬¨q) and (q‚ÇÄ ‚Üí q) implies (p ‚Üí ¬¨q‚ÇÄ)
      (for ([q‚ÇÄ (in-set (get-strongers q))])
        (add-exclusion! p q‚ÇÄ))
      (for ([p‚ÇÄ (in-set (get-strongers p))])
        (add-exclusion! p‚ÇÄ q))
      ;; exclusion is symmetric
      (add-exclusion! q p)))

  (define (get-weakers    [p : Symbol]) (hash-ref implication-table   p mk-‚àÖeq))
  (define (get-strongers  [p : Symbol]) (hash-ref implication-table‚Åª¬π p mk-‚àÖeq))
  (define (get-exclusions [p : Symbol]) (hash-ref exclusion-table     p mk-‚àÖeq))

  (define (o‚áío [p : Symbol] [q : Symbol])
    (cond [(eq? p q) '‚úì]
          [(‚àã (get-weakers p) q) '‚úì]
          [(‚àã (get-exclusions p) q) '‚úó]
          [else '?]))

  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ;;;;; Range
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (define range-table : (HashTable Symbol Symbol) (make-hasheq))
  (define partial-prims : (HashTable Symbol Natural) (make-hasheq))

  (define (set-range! [o : Symbol] [r : Symbol]) (hash-set-once! range-table o r))
  (define (set-partial! [o : Symbol] [n : Natural]) (hash-set! partial-prims o n))
  (define (get-conservative-range [o : Symbol]) (hash-ref range-table o (Œª () 'any/c)))


  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
  ;;;;; Arity
  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (define arity-table : (HashTable Symbol Arity) (make-hasheq))

  (define (update-arity! [o : Symbol] [a : Arity])
    (cond [(hash-ref arity-table o #f) =>
           (Œª ([a‚ÇÄ : Arity])
             (unless (arity-includes? a‚ÇÄ a)
               (hash-set! arity-table o (normalize-arity (list a‚ÇÄ a)))))]
          [else
           (hash-set! arity-table o a)]))

  (define (get-arity [o : Symbol])
    (hash-ref arity-table o (Œª () (error 'get-arity "nothing for ~a" o))))

  ;;;;; TODO stuff below migrated from `runtime` to here
  ;;;;; not the most appropriate place...

  ;; Return given value's arity, or `#f` if it's not a procedure
  (define V-arity : (-V ‚Üí (Option Arity))
    (match-lambda
      [(-Clo xs _ _ _) (formals-arity xs)]
      [(-Case-Clo clauses _ _)
       (remove-duplicates
        (for/list : (Listof Natural) ([clause clauses])
          (match-define (cons xs _) clause)
          (length xs)))]
      [(or (-And/C #t _ _) (-Or/C #t _ _) (? -Not/C?) (-St/C #t _ _) (? -One-Of/C?)) 1]
      [(-Ar guard _ _) (guard-arity guard)]
      [(? -st-p?) 1]
      [(-st-mk ùíæ) (get-struct-arity ùíæ)]
      [(? -st-ac?) 1]
      [(? -st-mut?) 2]
      [(? symbol? o) (hash-ref arity-table o)]
      [(-‚óè _) #f]
      [V
       (printf "Warning: call `V-arity` on an obviously non-procedure ~a" (show-V V))
       #f]))
  )

